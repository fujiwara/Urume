#!/usr/bin/env perl
use strict;
use warnings;
use 5.12.0;
use Config::Pit;
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON;
use Path::Class qw/ file /;

my $command = shift;
my $config = pit_get("urume.config", require => {
    endpoint => "Urume API endpoint",
});
my $ua = LWP::UserAgent->new( agent => "Mezasi/0.1" );
my $endpoint = $config->{endpoint};
$endpoint .= "/" if $endpoint !~ m{/$};
warn "endpoint: $endpoint\n";

usage() unless defined $command;
if ( my $func = main->can("_$command") ) {
    warn "command: $command\n";
    $func->(@ARGV);
}
else {
    usage();
}
exit;

sub url($) {
    my $path = shift;
    $endpoint . $path;
}

sub request($) {
    my $req = shift;
    warn $req->as_string if $ENV{DEBUG};
    $ua->request($req);
}

sub pp($) {
    my $res = shift;

    warn $res->as_string if $ENV{DEBUG};

    if ( $res->content_type =~ m{application/json} ) {
        my $obj = decode_json($res->content);
        print JSON->new->pretty->encode($obj);
    }
    elsif ( $res->content_type =~ m{text/html} ) {
        print $res->status_line, "\n";
    }
    else {
        print $res->status_line, "\n";
        print $res->content;
    }
}

sub _list {
    pp request GET url "vm/list";
}

sub _config {
    my $url = $endpoint . "config";
    pp request GET url "config";
}

sub _info {
    my $name = shift;
    usage("info") if !defined $name;

    pp request GET url "vm/info/$name";
}

sub _start {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/start/$name";
}

sub _stop {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/stop/$name";
}

sub _force_stop {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/force_stop/$name";
}

sub _remove {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/remove/$name";
}

sub _register {
    my ($public_key, $name, $base);
    GetOptions(
        "public-key|k=s" => \$public_key,
        "name|n=s"       => \$name,
        "base|b=s"       => \$base,
    );
    my $url = url "vm/register";
    usage("register") if !defined $base || !defined $name;

    my $key = file($public_key)->slurp;
    pp request(
        POST $url,
        Content_Type => "form-data",
        Content => [
            name => $name,
            base => $base,
            public_key => $key,
        ]
    );
}

sub _public_key {
    my $name = shift;
    my $file = shift;
    usage("public_key") if !defined $name;
    my $url = url "public_key/$name";

    if ( defined $file ) {
        my $key = file($file)->slurp;
        pp request(
            POST $url,
            Content_Type => "form-data",
            Content => [
                public_key => $key,
            ]
        );
    }
    else {
        pp request GET $url;
    }
}

sub _user_data {
    my $name = shift;
    my $file = shift;
    usage("user_data") if !defined $name;
    my $url  = url "user_data/$name";

    if ( defined $file ) {
        my $data = file($file)->slurp;
        pp request(
            POST $url,
            Content_Type => "form-data",
            Content => [
                user_data => $data,
            ]
        );
    }
    else {
        pp request GET $url;
    }
}

sub _ssh {
    my $name = shift;
    usage("ssh") if !defined $name;

    my $res = request GET url "vm/info/$name";
    if ( $res->code == 200 ) {
        my $vm  = decode_json($res->content);
        my @command = ("ssh", $vm->{ip_addr}, @_);
        print "@command\n";
        exec @command;
    }
    else {
        pp($res);
    }
}

sub usage {
    my $type = shift;
    say "Usage";
    $type //= "";
    if ( $type eq "register" ) {
        say "$0 register --name vmname --base baseimage --public-key ~/.ssh/id_rsa.pub";
    }
    elsif ( $type eq "public_key" ) {
        say "$0 public_key vmname path/to/id_rsa.pub";
    }
    elsif ( $type eq "user_data" ) {
        say "register: $0 user_data vmname path/to/user_data_file";
        say "retrieve: $0 user_data vmname";
    }
    elsif ( $type =~ /^(start|stop|force_stop|remove|info)$/ ) {
        say "$0 $type vmname";
    }
    elsif ( $type eq "ssh" ) {
        say "$0 ssh vmname [ssh options]";
    }
    else {
        say "$0 (list|info|register|start|stop|force_stop|remove|public_key|config|ssh|user_data)";
    }
    print "\n";
    exit 1;
}

