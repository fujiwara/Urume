#!/usr/bin/env perl
use strict;
use warnings;
use 5.12.0;
use Config::Pit;
use Getopt::Long qw(:config posix_default no_ignore_case gnu_compat);
use LWP::UserAgent;
use HTTP::Request::Common;
use JSON;
use Path::Class qw/ file /;

my $command = shift;
my $config = pit_get("urume.config", require => {
    endpoint => "Urume API endpoint",
});
my $ua = LWP::UserAgent->new( agent => "Mezasi/0.1" );
my $endpoint = $config->{endpoint};
$endpoint .= "/" if $endpoint !~ m{/$};
warn "endpoint: $endpoint\n";

usage() unless defined $command;
if ( my $func = main->can("_$command") ) {
    warn "command: $command\n";
    $func->(@ARGV);
}
else {
    usage();
}
exit;

sub url($) {
    my $path = shift;
    $endpoint . $path;
}

sub request($) {
    my $req = shift;
    warn $req->as_string if $ENV{DEBUG};
    $ua->request($req);
}

sub pp($) {
    my $res = shift;

    warn $res->status_line . "\n";
    warn $res->as_string if $ENV{DEBUG};

    if ($res->content_type =~ /json/) {
        my $obj = decode_json($res->content);
        print JSON->new->pretty->encode($obj);
    }
    else {
        print $res->content;
    }
}

sub _list {
    pp request GET url "vm/list";
}

sub _config {
    my $url = $endpoint . "config";
    pp request GET url "config";
}

sub _info {
    my $name = shift;
    usage("info") if !defined $name;

    pp request GET url "vm/info/$name";
}

sub _start {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/start/$name";
}

sub _stop {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/stop/$name";
}

sub _force_stop {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/force_stop/$name";
}

sub _remove {
    my $name = shift;
    usage("start") if !defined $name;
    pp request POST url "vm/remove/$name";
}

sub _register {
    my ($public_key, $name, $base, $user_data);
    GetOptions(
        "public-key|k=s" => \$public_key,
        "name|n=s"       => \$name,
        "base|b=s"       => \$base,
        "user-data|d=s"  => \$user_data,
    );
    my $url = url "vm/register";
    usage("register") if !defined $base || !defined $name;

    my $key  = $public_key ? file($public_key)->slurp : undef;
    my $data = $user_data  ? file($user_data)->slurp  : undef;
    pp request(
        POST $url,
        Content_Type => "form-data",
        Content => [
            name       => $name,
            base       => $base,
            public_key => $key,
            user_data  => $data,
        ]
    );
}

sub _public_key {
    my $name = shift;
    my $file = shift;
    usage("public_key") if !defined $name;
    my $url = url "public_key/$name";

    if ( defined $file ) {
        my $key = file($file)->slurp;
        pp request(
            POST $url,
            Content_Type => "form-data",
            Content => [
                public_key => $key,
            ]
        );
    }
    else {
        pp request GET $url;
    }
}

sub _user_data {
    my $name = shift;
    my $file = shift;
    usage("user_data") if !defined $name;
    my $url  = url "user_data/$name";

    if ( defined $file ) {
        my $data = file($file)->slurp;
        pp request(
            POST $url,
            Content_Type => "form-data",
            Content => [
                user_data => $data,
            ]
        );
    }
    else {
        pp request GET $url;
    }
}

sub _ssh {
    my $name = shift;
    usage("ssh") if !defined $name;

    my $res = request GET url "vm/info/$name";
    if ( $res->code == 200 ) {
        my $vm  = decode_json($res->content);
        my @command = ("ssh", $vm->{ip_addr}, @_);
        print "@command\n";
        exec @command;
    }
    else {
        pp($res);
    }
}

sub usage {
    my $type = shift;
    say "Usage";
    $type //= "";
    if ( $type eq "register" ) {
        say "mezasi register --name vmname --base baseimage --public-key path/to/id_rsa.pub --user-data /path/to/user.sh";
    }
    elsif ( $type eq "public_key" ) {
        say "mezasi public_key vmname path/to/id_rsa.pub";
    }
    elsif ( $type eq "user_data" ) {
        say "register: mezasi user_data vmname path/to/user_data_file";
        say "retrieve: mezasi user_data vmname";
    }
    elsif ( $type =~ /^(start|stop|force_stop|remove|info)$/ ) {
        say "mezasi $type vmname";
    }
    elsif ( $type eq "ssh" ) {
        say "mezasi ssh vmname [ssh options]";
    }
    else {
        say "mezasi (list|info|register|start|stop|force_stop|remove|public_key|config|ssh|user_data)";
    }
    print "\n";
    exit 1;
}

